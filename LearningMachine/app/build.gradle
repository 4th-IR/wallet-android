apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "com.learningmachine.android.app"
        minSdkVersion 19
        def buildNumber = System.getenv("BUILD_NUMBER")
        if (buildNumber != null) {
            versionCode buildNumber.toInteger()
            versionName "1.0"
            versionNameSuffix "-" + System.getenv("BUILD_NUMBER")
        } else {
            versionCode 1
            versionName "1.0"
            versionNameSuffix "-debug"
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        LearningMachineRelease {
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
            storeFile file('../keys/learningmachine.keystore.jks')
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
        }
    }


    productFlavors {
        dev {
            applicationId "com.learningmachine.android.app.dev"
        }
        staging {
            applicationId "com.learningmachine.android.app.staging"
        }

        production {
            applicationId "com.learningmachine.android.app"
        }
    }
    
    buildTypes {
        release {
            if (System.getenv("BUILD_NUMBER") != null) {
                versionNameSuffix "-" + System.getenv("BUILD_NUMBER")
            } else {
                versionNameSuffix "-nontravis"
            }
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + versionName + ".apk"))
                }
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.LearningMachineRelease
        }

        debug {
            buildConfigField "boolean", "DEBUG_BUILD", "true"
        }
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:recyclerview-v7:25.3.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'org.bitcoinj:bitcoinj-core:0.14.4'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
