apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.learningmachine.android.app"
        minSdkVersion 19
        targetSdkVersion 25
        versionName "1.0"
        def buildNumber = System.getenv("BUILD_NUMBER")
        if (buildNumber != null) {
            versionCode buildNumber.toInteger()
            versionNameSuffix "-" + buildNumber
        } else {
            versionCode 1
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        LearningMachineRelease {
            keyAlias System.getenv("SIGNING_KEY_ALIAS")
            keyPassword System.getenv("SIGNING_KEY_PASSWORD")
            storeFile file('../../keys/learning-machine.jks')
            storePassword System.getenv("SIGNING_STORE_PASSWORD")
        }
    }

    productFlavors {
        dev {
            applicationId "com.learningmachine.android.app.dev"
        }
        staging {
            applicationId "com.learningmachine.android.app.staging"
        }
        production {}
    }


    buildTypes {
        release {
            if (System.getenv("BUILD_NUMBER") == null) {
                versionNameSuffix "-nontravis"
            }

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-" + versionName + ".apk"))
                }
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.LearningMachineRelease
        }

        debug {
            if (System.getenv("BUILD_NUMBER") == null) {
                versionNameSuffix "-debug"
            }
            buildConfigField "boolean", "DEBUG_BUILD", "true"

        }
    }

    dataBinding {
        enabled = true
    }
}

final SUPPORT_VERSION = '25.3.1'
final DAGGER_VERSION = '2.7'
final BITCOINJ_VERSION = '0.14.4'
final TIMBER_VERSION = '4.5.1'
final JODATIME_VERSION = '2.8.2'
final RXJAVA_VERSION = '1.2.1'
final RXLIFECYCLE_VERSION = '1.0'
final RXLINT_VERSION = '1.2'

// test
final MOCKITO_VERSION = '2.2.11'

// These must be forced until everyone gets on the same page
// RxLifecycle: 3.0.1, ??: 2.0.1
final FINDBUGS_VERSION = '3.0.1'

configurations.all {
    resolutionStrategy {
        force "com.google.code.findbugs:jsr305:$FINDBUGS_VERSION"
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$SUPPORT_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_VERSION"
    compile "com.android.support:design:$SUPPORT_VERSION"
    compile 'com.android.support:multidex:1.0.1'
    compile "org.bitcoinj:bitcoinj-core:$BITCOINJ_VERSION"
    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    compile "com.jakewharton.timber:timber:$TIMBER_VERSION"
    compile "net.danlew:android.joda:$JODATIME_VERSION"
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile "io.reactivex:rxjava:$RXJAVA_VERSION"
    compile "io.reactivex:rxandroid:$RXJAVA_VERSION"
    compile "com.trello:rxlifecycle:$RXLIFECYCLE_VERSION"
    compile "com.trello:rxlifecycle-android:$RXLIFECYCLE_VERSION"
    compile "nl.littlerobots.rxlint:rxlint:$RXLINT_VERSION"


    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$MOCKITO_VERSION"
}
